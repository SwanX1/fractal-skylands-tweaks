buildscript {
  repositories {
    maven { url = 'https://maven.minecraftforge.net' }
    maven { url = 'https://repo.spongepowered.org/maven' }
    mavenCentral()
  }

  dependencies {
    classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true
    classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
  }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'org.spongepowered.mixin'

// This initializes the config. Please try to modify the variables in the config file, if needed, add more if you see a reason to.
ext.config = parseConfig(file('build.properties'))

version = config.VERSION
group = "${config.GROUP}.${config.ARTIFACT}"
archivesBaseName = "${config.ARCHIVES_BASE_NAME}-${config.MINECRAFT_VERSION}"

compileJava.options.encoding = "UTF-8"
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
  mappings channel: config.MAPPINGS_CHANNEL, version: config.MAPPINGS_VERSION
  copyIdeResources = true
  runs {
    configureEach {
      workingDirectory project.file('run')

      arg "-mixin.config=${config.ARTIFACT}.mixins.json"
      property 'forge.logging.markers', ''
      property 'forge.logging.console.level', 'debug'
      property 'forge.enabledGameTestNamespaces', config.MOD_ID

      mods {
        "${config.MOD_ID}" {
          source sourceSets.main
        }
      }
    }

    client {}

    server {
      args '--nogui'
    }

    data {
      workingDirectory project.file('run-data')
      args '--mod', config.MOD_ID, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
    }
  }
}

// sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
  maven {
    url 'https://www.cursemaven.com'
    content {
      includeGroup 'curse.maven'
    }
  }
  maven {
    url 'https://maven.theillusivec4.top/'
    content {
      includeGroup 'top.theillusivec4.curios'
    }
  }
  maven {
    url 'https://maven.blamejared.com/'
    content {
      includeGroup 'mezz.jei'
    }
  }
  maven {
    url 'https://modmaven.dev'
    content {
      includeGroup 'mezz.jei'
    }
  }

  maven { url "https://maven.tterrag.com/" }
}

dependencies {
  minecraft "net.minecraftforge:forge:${config.MINECRAFT_VERSION}-${config.FORGE_VERSION}"
  implementation fg.deobf("com.simibubi.create:create-${config.MINECRAFT_VERSION}:${config.CREATE_VERSION}:slim") { transitive = false }
  implementation fg.deobf("com.jozufozu.flywheel:flywheel-forge-${config.MINECRAFT_VERSION}:${config.FLYWHEEL_VERSION}")
  implementation fg.deobf("com.tterrag.registrate:Registrate:${config.REGISTRATE_VERSION}")
  implementation fg.deobf("curse.maven:farmersdelight-398521:${config.FARMERS_DELIGHT_FILE}")
  implementation fg.deobf("curse.maven:mutil-351914:${config.MUTIL_FILE}")
  implementation fg.deobf("curse.maven:tetra-289712:${config.TETRA_FILE}")
  implementation fg.deobf("curse.maven:ftb-quests-289412:${config.FTB_QUESTS_FILE}")
  implementation fg.deobf("curse.maven:ftb-library-404465:${config.FTB_LIBRARY_FILE}")
  implementation fg.deobf("curse.maven:ftb-teams-404468:${config.FTB_TEAMS_FILE}")
  implementation fg.deobf("curse.maven:architectury-419699:${config.ARCHITECTURY_FILE}")

  runtimeOnly fg.deobf("top.theillusivec4.curios:curios-forge:${config.CURIOS_VERSION}")
  compileOnly fg.deobf("top.theillusivec4.curios:curios-forge:${config.CURIOS_VERSION}:api")
  compileOnly fg.deobf("mezz.jei:jei-${config.MINECRAFT_VERSION}-forge-api:${config.JEI_VERSION}")
  runtimeOnly fg.deobf("mezz.jei:jei-${config.MINECRAFT_VERSION}-forge:${config.JEI_VERSION}")

  // Dev only
  implementation fg.deobf("curse.maven:not-enough-crashes-forge-442354:4684047")
  implementation fg.deobf("curse.maven:spark-361579:4738952")

  annotationProcessor "org.spongepowered:mixin:${config.MIXIN_VERSION}:processor"
}

jar {
  manifest {
    attributes([
      'Specification-Title'   : config.TITLE,
      'Specification-Vendor'  : config.AUTHOR,
      'Specification-Version' : '1',
      'Implementation-Title'  : config.TITLE,
      'Implementation-Version': config.VERSION,
      'Implementation-Vendor' : config.AUTHOR,
      'MixinConfigs'          : "${config.ARTIFACT}.mixins.json",
    ])
  }
}

jar.finalizedBy('reobfJar')

mixin {
  add sourceSets.main, "${config.ARTIFACT}.refmap.json"
}

// Config parse function
def parseConfig(File config) {
  config.withReader {
    def prop = new Properties()
    prop.load(it)
    return new ConfigSlurper().parse(prop)
  }
}
